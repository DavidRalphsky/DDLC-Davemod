label ch4_exclusive_natsuki:
    play music t6 fadeout 2.0
    scene bg house with wipeleft_scene
    "It's saturday already."
    "I spend the whole day anxiously awaiting Natsuki's arrival."
    "Before I know it, she texts me to let me know she's outside the front door."
    "Without delay, I open the front door to let her in."
    show natsuki 2bj zorder 2 at t11
    mc "..."
    n "'Sup?"
    mc "...Hey."
    "I don't know what I was expecting, but seeing Natsuki in something other than her school uniform totally threw me off."
    "Seeing her in such cute clothes makes the uniform seem totally unfitting in comparison."
    n 4bc "Jeez, don't make it feel so awkward already!"
    n "It's gonna be a long afternoon, so don't be weird just because you're not used to seeing me outside of school."
    n "Anyway, I'm coming in."
    scene bg kitchen
    show natsuki 1bj zorder 2 at t11
    with wipeleft
    mc "I see you brought a lot of stuff..."
    "Natsuki is carrying a large bag that is probably full of baking supplies."
    n 2bj "Well, I didn't want to come all this way to find out that your kitchen isn't equipped for the job."
    n "You bought everything I asked you to, right?"
    mc "Yeah, I did."
    "Yesterday, Natsuki asked me to buy a bunch of ingredients if I didn't already have them at home."
    n 2bl "Good!"
    n "Glad I could count on you to do your part."
    mc "Well...of course."
    "I'm surprised to hear Natsuki suddenly say that, rather than something snarky like she usually does."
    "Could it be that she is a little different outside of school after all?"
    mc "Anyway, let's go to the kitchen..."
    n 2by "What, you're not even gonna offer to take this heavy bag from me?"
    n "Where's your hospitality, [player]?"
    mc "Come on..."
    mc "Since when did I need to be a gentleman?"
    "I grab the bag Natsuki holds out to me."
    mc "Ghk--"
    mc "This is ridiculously heavy--!"
    n 4bz "Ahaha!"
    n "I carried that all the way here."
    n 4bl "Are you impressed?"
    mc "I see now..."
    mc "Yeah, I am impressed, Natsuki."
    mc "It seems like I always underestimate you."
    n 4by "Ehehe~"
    n "It's because I'm so small, isn't it?"
    n 4ba "You jerk."
    "Natsuki hits a fist into my chest."
    mc "Hey, hey."
    mc "Your size has nothing to do with it."
    mc "Do you really hate being small that much?"
    n 1bk "Eh?"
    n "Um..."
    n 1bc "It's not like I hate it..."
    n "I mean, sometimes I like proving people wrong when they only think I'm worth my size."
    n 1ba "It's fun when I get to be small and also better than other people."
    n "But..."
    n 5bw "...Jeez, never mind!"
    n "What are you making me say?"
    n 5bq "Don't think you can make me talk about weird things just because we're not at school!"
    n "Are we getting started, or what? There's a lot of stuff I gotta teach you."
    mc "Ahaha."
    n 5bn "What??"
    mc "That's a little bit more like you."
    mc "You're more fun when you just speak your mind like that."
    n 1bm "H-Hey!"
    n "Now you {i}are{/i} treating me like a kid!"
    n "I was just trying to be a little nicer to you, you know."
    if persistent.yuri_dead == 0:
        n 1br "And just because I don't have a mature and sexy figure like Yuri doesn't mean you should treat me like--"
        n 1bo "A-Ah--"
        "Natsuki catches her words, and her face turns red."
        mc "Natsuki..."
        n 1bp "Forget it!"
        n "I didn't say anything!"
        mc "I should apologize."
        n 1bh "Eh?"
        mc "I appreciate that you were trying to be nicer."
        mc "I should have been a little more considerate, too."
        mc "But also..."
        mc "If that's what you're thinking, then you should know that there are tons of guys who are into body types like yours."
        n 1bq "Ah..."
        n "How would...you know that, anyway?"
        mc "Just trust me on this one."
        n 5bx "...Gross."
        mc "Hey!"
        mc "Was that to me?"
        n 5bw "Who else?"
        mc "Man..."
        mc "Let's just get started already."
        n 2bl "Ahaha!"
        n "You get all sour when a girl calls you gross."
        n 2bd "I finally found your weakness, [player]."
        "Natsuki smiles deviously."
        mc "Please spare me..."
        "Well, if Natsuki decides to dish out more insults like that, there's no way I'm not fighting back."
        "But she's satisfied enough for now, finally starting to pull things out of her bag so we can get started."
    else:
        "I just smile deviously."

    scene bg kitchen
    with wipeleft_scene
    "Before long, the whole kitchen is a mess."
    "Spoons, dirty bowls, flour, spilled fluid, and plastic bags are strewn about every countertop."
    "The mixer isn't big enough to make all the batter at once, so we've had to do it several times."
    "Meanwhile, Natsuki is babysitting all of my movements to make sure I don't mess up her precious baking."
    show natsuki 2bk zorder 2 at t11
    n "[player], where did you put the food coloring?"
    n "The batter's going in the oven soon, so I need to fill the trays."
    mc "I think it's still in the bag next to the table."
    mc "What are you using it for?"
    n 4bl "To color the batter, of course!"
    n 4bj "I'm making each tray a different color."
    n "That way, even if the flavors aren't different, everyone can still pick their favorite."
    mc "Ah, that's a cute idea."
    mc "Are we doing anything like that with the icing?"
    n 4bk "Do you want to?"
    mc "Ah..."
    mc "You're asking me?"
    mc "I don't really have a preference, so..."
    n 1bm "Come on..."
    n "You're not putting any heart into this at all!"
    n "Can't you at least try to have fun?"
    mc "I'm having fun..."
    "I'm not really sure what Natsuki is trying to get out of me."
    "Meanwhile, I see her separate the batter into smaller bowls and put a few drops of food coloring into each."
    mc "Ah, that does look pretty cool."
    n 2bj "See?"
    n "It's not like baking is just about following instructions."
    n "The presentation is where you get to be creative and have the most fun."
    n "It's a million times more worth it in the end if just looking at it makes everyone's eyes lighten up."
    mc "Like the ones you made on my first day, huh?"
    if persistent.sayori_dead == 0:
        "I recall Natsuki proudly presenting her cat-shaped cupcakes, and Sayori and Monika's delighted expressions."
    else:
        "I recall Natsuki proudly presenting her cat-shaped cupcakes, and Monika's delighted expression."
    "I wonder if I can make Natsuki proud like that, too."
    mc "Yeah..."
    mc "Maybe I will use the food coloring, then."
    n "Sounds like you're starting to understand."
    n "Just make sure you completely finish mixing the icing before you mess with the food coloring."
    mc "Yeah, it's getting there."
    "We were using the electric mixer for the batter, so I got stuck with a whisk and a huge bowl for the icing."
    n 4bc "Eh?"
    n "The icing's still all lumpy!"
    n "Are you even trying?"
    mc "Well, yeah..."
    mc "It'll just take a little longer."
    n 4bg "Jeez, I'll be here all night if you do it like that."
    n "Here, look."
    "Natsuki grabs the whisk from me and uses her other hand to tilt the bowl back."
    n 4be "You really need to...beat...the crap out of it!"
    "After a few seconds, the consistency of the icing has already improved."
    n 4ba "See?"
    "As if to emphasize, Natsuki sticks a finger in the icing and pops it in her mouth."
    "I reluctantly start to do the same."
    n 1bh "Hey!"
    "Natsuki suddenly grabs my wrist."
    n 4bh "I don't want {i}your{/i} gross fingers in my icing."
    mc "Your icing, eh?"
    mc "Are you forgetting who did all the work?"
    "I start to fight back, trying to inch my finger toward the bowl."
    n 4by "Don't make me beat the crap out of you next!"
    mc "I'd like to see you try!"
    "I push harder, just enough for my finger to reach the icing."
    "I triumphantly scoop some with my finger just as Natsuki tugs with all her might."
    mc "Ah--!"
    "The force of Natsuki pulling me causes me to stumble, making her stumble in turn."
    n 1bx "Gross!"
    n "You got it on my face!"
    mc "Whose fault is that?!"
    "There's a big glob of icing on Natsuki's cheek."
    n 1bw "Nnn--"
    "She tries to reach it with her tongue, but it's too far away."
    n 1br "Jeez..."
    n "You know what?"
    n 4bd "Take this!"
    "Natsuki instead wipes it off with her finger before shoving her finger toward my own face."
    mc "You wish--!"
    "I'm faster."
    "I grab her wrist with my hand before it reaches my face."
    "Natsuki tries to use her other hand to fight back, but I grab that one as well."
    $ persistent.clear[4] = True
    scene n_cg3_base
    show n_cg3_exp1
    show n_cg3_cake
    with dissolve_cg
    n "Ahahaha! Stop!"
    mc "Not until you apologize for calling me gross!"
    n "Fine, fine!"
    n "I'm sorry for calling you gross."
    n "You know I don't mean it."
    n "It's just fun seeing you react to it."
    n "...You do that to me all the time, you know!"
    n "Saying dumb things just to get a reaction out of me."
    n "You really shouldn't tease girls like that."
    mc "Is that so?"
    mc "In that case, I probably shouldn't do this, either..."
    show n_cg3_cake at cgfade
    hide n_cg3_cake
    "I take Natsuki's finger and put it in my mouth, licking off the icing."
    show n_cg3_exp1 at cgfade
    show n_cg3_exp2 at cgfade
    hide n_cg3_exp1
    n "W-W-What--?"
    n "D-Did you seriously just--"
    n "A-Ah--"
    "Natsuki is so surprised that she can't even figure out how to get mad at me."
    "Her face is entirely red."
    hide n_cg3_exp2
    n "[player]..."
    n "You really shouldn't do that kind of thing to girls...unless you really like them..."
    n "You know that...right?"
    mc "..."
    "What kind of question is she asking me, just like that?"
    "How did the mood turn to this so quickly?"
    mc "I..."
    "Natsuki gazes at me in silence."
    "I notice her shallow breaths."
    "Why am I starting to feel dizzy...?"
    n "Eh?!"
    scene bg kitchen with dissolve_cg
    "Out of nowhere, the fire alarm starts going off."
    "Natsuki rushes over to the oven."
    mc "Is something burning?"
    mc "I thought you didn't put the cupcakes in yet."
    show natsuki 1bw zorder 2 at t11
    n "{i}*Cough*{/i}"
    n "No wonder..."
    n 1bb "You left a dirty tray in here, dummy!"
    n "How could you make a mistake like that?"
    mc "You should have checked before turning the oven on!"
    n 1bs "Don't blame me for your mistakes!"
    n "Jeez..."
    "Natsuki uses an oven mitt to grab the blackened tray out of the oven."
    "She sets it on top of the stove."
    "In another moment, the fire alarm stops."
    n 1bq "Anyway..."
    n "I'm...putting them in the oven now."
    mc "Yeah..."
    "The tension from the moment before still lingers over our heads."
    "But the moment has already been lost."
    "I watch as Natsuki slides the cupcake trays into the oven."
    "Then, I reluctantly pick up the whisk and continue with the icing, like nothing ever happened."

    scene bg kitchen
    show natsuki 4bz zorder 2 at t11
    with wipeleft_scene
    n "Ahh, that smells so good!"
    "The cupcakes are ready to be pulled out of the oven."
    "As soon as Natsuki opens the oven door, a blast of sweet-smelling warm air fills the room."
    n 4bl "Look at how cute they all look!"
    "She proudly shows off the different-colored cupcakes in each of the trays."
    mc "They'll look even better once we add the icing."
    n 2ba "Not like you need to tell {i}me{/i} that!"
    n "I brought decorating stuff, so I hope you can get creative."
    n "Here, scoop the icing into these bags."
    "Natsuki hands me some plastic bags."
    n 2bj "I have these nozzles that will make it look nice and fluffy."
    n "This one can even make flowers!"
    n "We probably won't be using it this time, though."
    mc "What's this one for?"
    "I pick up one of the nozzles that has a much thinner tip than the others."
    n 4bk "That one's really thin, so you can use it to make stripes or other patterns."
    n "But you can also use it to write stuff on a cake."
    n "Like, 'happy birthday!' or whatever."
    mc "Huh, I see..."
    mc "That gives me an idea, actually."
    n "Eh?"
    mc "Well, it's a literature event, right?"
    mc "We could make it more literature-themed by writing a different word on each of the cupcakes."
    mc "It would be fun to see people choose their cupcake based on a word they like."
    n 1bq "Uu..."
    mc "Hm?"
    n "I was kind of expecting you to say something really stupid..."
    n 1bs "But that's actually...a really cute idea, so..."
    mc "Ahaha."
    mc "Maybe I'm getting it from you."
    n 5bh "W-What's that supposed to mean?"
    n "I'm not cute!"
    mc "Come on..."
    mc "We're not at school, nobody's judging."
    mc "You can't dress and act like this and not expect me to think you're cute."
    n 5bs "W-Well..."
    "Natsuki's voice trails off."
    n "Same with you..."
    mc "Eh?"
    mc "Did you say something?"
    n 1bw "N-No, nothing!"
    n "Let's just do the icing!"
    "Natsuki picks up the pace and fastens a nozzle onto each of the bags."
    n 1bh "There's a lot to do, so we shouldn't be wasting time!"
    n "Here, I'll show you how to do it."
    "Without giving me a chance to think about before, Natsuki quickly moves on."
    "She shows me how to apply the icing, and then we each get to work."

    scene bg kitchen with wipeleft_scene
    "When we're finally finished, Natsuki puts them all side by side to admire our work."
    show natsuki 4bl zorder 2 at t11
    n "Look at how pretty they are together!"
    mc "Yeah, they are, aren't they?"
    n 1bm "Uu... I wish I could have one now!"
    mc "Well, there's no reason you can't, right?"
    mc "I don't see any harm in that."
    n 1bc "Well, yeah, but..."
    n "My dad's making dinner tonight, so I really need to save my appetite."
    mc "Ahaha."
    if persistent.sayori_dead == 0:
        mc "Sayori's the exact opposite in that regard."
        mc "If she was here, we'd probably be down ten cupcakes already."
        mc "And she would still eat dinner."
    else:
        mc "I dunno."
        mc "I think I could eat a bunch..."
        mc "And dinner too!"
    n 4bg "Come on, that's just unhealthy!"
    n 4bs "Besides, when my dad cooks, I need to eat as much of it as I can..."
    n 4bq "...Well, anyway!"
    n 4bc "I was hoping we would have time for manga, but I need to be home for dinner..."
    mc "Ah, already?"
    mc "That's a shame."
    n 1bg "It's your fault for working so slowly!"
    n "You should have thought about that."
    n "It's not like you'll always have this chance."
    mc "Man..."
    "As usual, Natsuki places the blame on me."
    n 2bk "You can bring the cupcakes tomorrow, right?"
    if persistent.sayori_dead == 0:
        n "If you and Sayori each carry some, then you can probably do it in one trip."
        mc "Yeah, I can do that."
        mc "And don't worry, I won't let her eat any."
        n 2ba "Ahaha."
        n "I wish she would listen to me the way she listens to you."
        mc "Ah..."
        mc "Yeah."
        show natsuki zorder 1 at thide
        hide natsuki
        if persistent.sayori_hearttoheart == 1:
            "I again think back to the conversation I had with Sayori earlier today."
            "I felt so helpless."
            "Sayori always does listen to me, but at that point it felt like she couldn't listen to me at all."
    else:
        mc "Yeah, I can do that."
        mc "And don't worry, I won't eat any."
        n 2ba "Ahaha."
    show natsuki 4bl zorder 2 at t11
    n "Okay, I'm all packed up."
    n "Good work today!"
    mc "You too."
    mc "I'll walk you out...I guess."

    scene bg house with wipeleft_scene
    "Just like that, Natsuki is already about to leave."
    "It feels like the afternoon went by in a flash."
    "More than that..."
    "Did I even take the opportunity to get closer to her, like I wanted?"
    show natsuki 1bh zorder 2 at t11
    n "Well..."
    n "I guess I'll be off, then..."
    n 1bq "Thanks for all the help and everything..."
    n "I'll see you tomorrow."
    mc "Wait, Natsuki."
    n 1bh "Eh?"
    mc "What you said before...about not always having this chance."
    mc "It doesn't have to be that way at all!"
    mc "I had fun today."
    mc "You showed me how fun baking can be, like you wanted."
    mc "But aside from that..."
    mc "You can come over anytime, okay?"
    mc "I think that if possible, I'd like to spend more time like this."
    mc "If you want to read manga, or go out somewhere--"
    n 1bm "Um--"
    n "Do you...really mean that?"
    "Natsuki looks at me tensely, like she's trying to hide her expression."
    mc "Yeah."
    mc "I want to spend more time with you."
    n 1bq "[player]..."
    n "I thought you only cared about getting this done..."
    n 1br "Uu..."
    n 1bn "I'm sorry I had to leave so early today."
    n "I really didn't want to!"
    n "I would really...stay here longer if I could."
    n "I feel the same way as you, so..."
    stop music fadeout 2.0
    show natsuki 1bi at face with dissolve
    "Natsuki suddenly gets closer to me."
    mc "Wait, Natsuki--"
    "Standing inches from me, Natsuki looks up at me."
    "I feel her fingers gently clutch at the sides of my shirt, as if holding onto me."
    "Her rose-colored cheeks and matching eyes fill my vision, along with her slightly-parted lips."
    "What is happening...?"
    "My head starts to go dizzy as I feel her soft breaths against me."
    n 1bh "I've felt it..."
    n "For a while now..."
    n 1bo "--!!"
    show natsuki zorder 2 at t11
    "Natsuki suddenly jumps back."
    if persistent.sayori_dead == 0:
        n "S-Sayori?!"
        mc "Eh?!"
        show natsuki zorder 2 at t22
        show sayori 1bl zorder 3 at f21
        s "Ah..."
        s "H-Hi, [player]..."
        mc "Sayori--!"
        mc "Just now, we weren't--"
        s 1bq "Ehehe~"
        s "It's okay, [player]."
        s 1ba "I just stopped by to say hi~"
        show sayori zorder 2 at t21
        show natsuki zorder 3 at f22
        n 5bq "A-Ah..."
        n "Well..."
        n 5bw "Y-You should have come a little earlier!"
        n "I'm already on my way out, so..."
        show natsuki zorder 2 at t22
        show sayori zorder 3 at f21
        s 1bh "Aw, really?"
        s "That's too bad..."
        show sayori zorder 2 at t21
        show natsuki zorder 3 at f22
        n 2bq "Yeah, well..."
        n "I'll still see you at the festival tomorrow, so it's fine."
        n 2bb "Just don't eat any cupcakes before then!"
        n "Anyway, later!"
        show natsuki at lhide
        hide natsuki
        "Clearly flustered, Natsuki hurries off, and Sayori waves goodbye."
    else:
        n 5bq "I--uh!"
        mc "Natsuki?"
        "I'm suddenly confused..."
        "Something's wrong..."
        "Natsuki looks like she's in pain, and I feel the same way."
        n 5bw "I have to go!"
        mc "N-Natsuki!"
        show natsuki at lhide
        hide natsuki
        "I call after her, but she's already running down the street."
        "That was strange..."
        "A wave of nausea hits me..."
        "I crumple onto the ground..."
        "What's going on...?"
        
    call natsuki_exclusive4
    return
    